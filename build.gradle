plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

group = 'org.freedcamp'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.codeborne:selenide:7.11.1'
    implementation 'io.rest-assured:rest-assured:5.5.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.0'
    implementation 'io.qameta.allure:allure-junit5:2.29.0'
    implementation 'io.qameta.allure:allure-rest-assured:2.29.0'
    implementation 'org.projectlombok:lombok:1.18.28'
    implementation 'org.aeonbits.owner:owner:1.0.12'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'net.datafaker:datafaker:2.4.3'
    implementation 'org.assertj:assertj-core:3.24.2'
    implementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    implementation 'org.awaitility:awaitility:4.2.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.28'

    testImplementation 'io.qameta.allure:allure-junit5:2.29.0'
    testImplementation 'io.qameta.allure:allure-selenide:2.29.0'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.8'
}

configurations {
    testRuntimeClasspath {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
}

def credsFileProp = findProperty('credsFile') as String

def forks = (findProperty('forks') ?: '1') as int
def junitThreads = (findProperty('junitThreads') ?: '2') as int

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    maxParallelForks = forks
    forkEvery = 0

    systemProperty 'junit.jupiter.execution.parallel.enabled', 'true'
    systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
    systemProperty 'junit.jupiter.execution.parallel.config.strategy', 'fixed'
    systemProperty 'junit.jupiter.execution.parallel.config.fixed.parallelism', junitThreads.toString()

    systemProperty 'allure.results.directory', "${buildDir}/allure-results"

    if (credsFileProp) {
        systemProperty 'credsFile', credsFileProp
    }

    testLogging { showStandardStreams = true }
}

tasks.register('uiTest', Test) {
    description = 'Run UI tests in parallel'
    useJUnitPlatform {
        includeTags 'UI'
    }
}

tasks.register('apiTest', Test) {
    description = 'Run API tests in parallel'
    useJUnitPlatform {
        includeTags 'API'
    }
}

tasks.register('e2eTest', Test) {
    description = 'Run E2E tests in parallel'
    useJUnitPlatform {
        includeTags 'E2E'
    }
}

tasks.named('test') {
    description = 'Run all tests in parallel'
}

def includeTagsProp = System.getProperty("includeTags", "").trim()
def excludeTagsProp = System.getProperty("excludeTags", "").trim()

tasks.withType(Test).configureEach {
    useJUnitPlatform {
        def includes = includeTagsProp.split(',')*.trim().findAll { it } // список без порожніх
        if (!includes.isEmpty()) {
            includeTags(includes as String[])
        }

        def excludes = excludeTagsProp.split(',')*.trim().findAll { it }
        if (!excludes.isEmpty()) {
            excludeTags(excludes as String[])
        }
    }
}


allure {
    version = '2.29.0'
    adapter {
        frameworks {
            junit5 { enabled = true }
        }
        autoconfigure = false
        aspectjWeaver = true
    }
}